/*
 * This file is part of Piper for Burp Suite (https://github.com/silentsignal/burp-piper)
 * Copyright (c) 2018 Andras Veres-Szentkiralyi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.5"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.10"
    implementation "org.snakeyaml:snakeyaml-engine:1.0"
    implementation "com.esotericsoftware:minlog:1.3"

    // protobuf-lite ONLY
    implementation "com.google.protobuf:protobuf-lite:3.0.0"

    // Burp provides this
    compileOnly "net.portswigger.burp.extender:burp-extender-api:1.7.22"

    testCompile "org.testng:testng:6.9.10"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.4"
        apiVersion = "1.4"
        freeCompilerArgs += ["-Xopt-in=kotlin.ExperimentalStdlibApi"]
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }

    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/source/proto/main/javalite"
        }
        resources {
            // THIS FIXES defaults.yaml
            srcDirs += "src/main/yaml"
        }
    }
}

shadowJar {
    archiveClassifier.set("")

    // Prevent Burp protobuf collisions
    relocate "com.google.protobuf", "burp.shaded.protobuf"
}

jar {
    enabled = false
}

build.dependsOn shadowJar
